vga_set_read_plane:
        ;---------------------------------
        ; スタックフレームの構築
        ;---------------------------------
                                            ;    + 8| プレーン (インデックス)
                                            ; ------+---------------
        push    ebp                         ;    + 4| EIP (戻り番地)
        mov     ebp, esp                    ; EBP+ 0| EBP (元の値)
                                            ;-------+---------------

        ;---------------------------------
        ; レジスタの保存
        ;---------------------------------
        push    eax
        push    edx

        ;---------------------------------
        ; 読み込みプレーンの選択
        ;---------------------------------
        mov     ah, [ebp + 8]
        and     ah, 0x03
        mov     al, 0x04
        mov     dx, 0x03CE
        out     dx, ax

        ;---------------------------------
        ; レジスタの復帰
        ;---------------------------------
        pop     edx
        pop     eax

        ;---------------------------------
        ; スタックフレームの破棄
        ;---------------------------------
        mov     esp, ebp
        pop     ebp

        ret

vga_set_write_plane:
        ;---------------------------------
        ; スタックフレームの構築
        ;---------------------------------
                                            ;    + 8| プレーン (ビット指定)
                                            ; ------+---------------
        push    ebp                         ;    + 4| EIP (戻り番地)
        mov     ebp, esp                    ; EBP+ 0| EBP (元の値)
                                            ;-------+---------------

        ;---------------------------------
        ; レジスタの保存
        ;---------------------------------
        push    eax
        push    edx

        ;---------------------------------
        ; 書き込みプレーンの選択
        ;---------------------------------
        mov     ah, [ebp + 8]
        and     ah, 0x0F
        mov     al, 0x02
        mov     dx, 0x03C4
        out     dx, ax

        ;---------------------------------
        ; レジスタの復帰
        ;---------------------------------
        pop     edx
        pop     eax

        ;---------------------------------
        ; スタックフレームの破棄
        ;---------------------------------
        mov     esp, ebp
        pop     ebp

        ret

vram_font_copy:
        ;---------------------------------
        ; スタックフレームの構築
        ;---------------------------------
                                            ;    +20| 描画色
                                            ;    +16| プレーン(ビット指定)
                                            ;    +12| VRAMアドレス
                                            ;    + 8| フォントアドレス
                                            ; ------+---------------
        push    ebp                         ;    + 4| EIP (戻り番地)
        mov     ebp, esp                    ; EBP+ 0| EBP (元の値)
                                            ; ------+---------------

        ;---------------------------------
        ; レジスタの保存
        ;---------------------------------
        push    eax
        push    ebx
        push    ecx
        push    edx
        push    esi
        push    edi

        ;---------------------------------
        ; 処理の開始
        ;---------------------------------
        mov     esi, [ebp + 8]
        mov     edi, [ebp +12]
        movzx   eax, byte [ebp +16]
        movzx   ebx, word [ebp + 20]

        test    bh, al
        setz    dh
        dec     dh

        test    bl, al
        setz    dl
        dec     dl

        ;---------------------------------
        ; 16ドットフォントのコピー
        ;---------------------------------
        cld

        mov     ecx, 16
.10L:
        ;---------------------------------
        ; フォントマスクの作成
        ;---------------------------------
        lodsb
        mov     ah, al
        not     ah

        ;---------------------------------
        ; 前景色
        ;---------------------------------

        and     al, dl

        ;---------------------------------
        ; 背景色
        ;---------------------------------
        test    ebx, 0x0010
        jz      .11F
        and     ah, [edi]
        jmp     .11E
.11F:

        and     ah, dh
.11E:

        ;---------------------------------
        ; 前景色と背景色を合成
        ;---------------------------------
        or      al, ah

        ;---------------------------------
        ; 新しい値を出力
        ;---------------------------------
        mov     [edi], al

        add     edi, 80
        loop    .10L
.10E:

        ;---------------------------------
        ; レジスタの復帰
        ;---------------------------------
        pop     edi
        pop     esi
        pop     edx
        pop     ecx
        pop     ebx
        pop     eax

        ;---------------------------------
        ; スタックフレームの破棄
        ;---------------------------------
        mov     esp, ebp
        pop     ebp

        ret

vram_bit_copy:
        ;---------------------------------------
        ; スタックフレームの構築
        ;---------------------------------------
                                                ; EBP+20| 色（背景/前景色）
                                                ; EBP+16| プレーン（ビット指定）
                                                ; EBP+12| VRAMアドレス
                                                ; EBP+ 8| 出力ビットパターン
	                                        ; ------+----------------
        push    ebp				; EBP+ 4| EIP（戻り番地）
        mov     ebp, esp			; EBP+ 0| EBP（元の値）
						; ------+----------------

        ;---------------------------------------
        ; レジスタの保存
        ;---------------------------------------
        push    eax
        push    ebx
        push    edi

        ;---------------------------------------
        ; 処理の開始
        ;---------------------------------------
        mov     edi, [ebp + 12]                 ; EDI = VRAMアドレス;
        movzx   eax, byte [ebp + 16]            ; EAX = プレーン(ビット指定);
        movzx   ebx, word [ebp + 20]            ; EBX = 表示色;

        test    bl, al                          ; ZF = (前景色 & プレーン);
        setz    bl                              ; BL = ZF ? 0x01 : 0x00;
        dec     bl                              ; BL--; // 0x00 or 0xFF

        ;---------------------------------------
        ;  マスクデータの作成
        ;---------------------------------------
        mov     al, [ebp + 8]                   ; AL = 出力ビットパターン;
        mov     ah, al                          ; AH ~= AL;     // !出力ビットパターン(ビット反転)
        not     ah

        ;---------------------------------------
        ; 現在地の出力を取得
        ;---------------------------------------
        and     ah, [edi]                       ; AH = 現在地 & !出力ビットパターン;
        and     al, bl                          ; AL = 表示色 &  出力ビットパターン;
        or      al, ah                          ; AL |= AH;

        ;---------------------------------------
        ; 新しい値を出力
        ;---------------------------------------
        mov     [edi], al                       ; [EDI] = BL;   // プレーンに書き込む

        ;---------------------------------------
        ; レジスタの復帰
        ;---------------------------------------
        pop     edi
        pop     ebx
        pop     eax

        ;---------------------------------------
        ; スタックフレームの破棄
        ;---------------------------------------
        mov     esp, ebp
        pop     ebp

        ret